plugins {
  id 'java'
  id 'maven-publish'
  id "com.jfrog.artifactory" version "3.1.1"
}
println 'Compiling Carbon 2 via Gradle ver. ' + gradle.gradleVersion

sourceCompatibility = '1.7'
ext.spigotVersion = '1.8.8-R0.1-SNAPSHOT'
ext.version = '2.0'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

repositories {
    mavenCentral()
    
    maven {
        name 'Spigot'
        url 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    
    maven {
        name 'dmulloy2-repo'
        url 'http://repo.dmulloy2.net/content/groups/public/'
    }
}

if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}

task sourceJar (type : Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
    baseName "Carbon2"
}

artifacts {
    archives sourceJar
    archives jar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.lastabyss'
            artifactId 'Carbon2'
            version version + '-SNAPSHOT'
            from components.java
            artifact sourceJar {
                classifier = 'sources'
            }
        }
    }
}

artifactoryPublish.skip = false

artifactory {
    contextUrl = 'http://localhost:8081/artifactory'
    publish {
      repository {
        repoKey = 'libs-snapshot-local'
        username = '${artifactory_user}'
        password = '${artifactory_password}'
      }
      defaults {
        publications('mavenJava')
      }
    }
    resolve {  
      repoKey = 'repo'
    }
}

file('libs').mkdirs()

ant.get src: 'https://dl.dropboxusercontent.com/u/71299154/Carbon2/spigot.jar', dest: file('libs'), verbose: false, skipexisting: true

dependencies {
    compile files('libs/spigot.jar')
    compile 'org.bukkit:bukkit:' + project.ext.spigotVersion
    compile 'com.comphenix.protocol:ProtocolLib:3.6.5-SNAPSHOT'
}
